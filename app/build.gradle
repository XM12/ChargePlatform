plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
apply from: rootProject.file("channel.gradle")

android {
    namespace rootProject.ext.android['applicationId']
    compileSdk rootProject.ext.android['defualt_compileSdk']

    def code = getVersionCode()
    def name = getVersionName()

    defaultConfig {
        applicationId rootProject.ext.android['applicationId']
        minSdk rootProject.ext.android['defualt_minSdk']
        targetSdk rootProject.ext.android['defualt_targetSdk']

        versionCode code
        versionName name

        multiDexEnabled true
        resConfigs("en", "zh", "zh-rCN")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    signingConfigs {
        debug {
            storeFile file('..\\key\\charge.jks')
            storePassword 'charge'
            keyPassword 'charge'
            keyAlias 'key0'
            v1SigningEnabled true
            v2SigningEnabled true
        }

        release {
            storeFile file('..\\key\\charge.jks')
            storePassword 'charge'
            keyPassword 'charge'
            keyAlias 'key0'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        local {
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.release'
            buildConfigField "String", "LOCAL_URL", rootProject.ext.api['local_url']
            buildConfigField "String", "LOCAL_PERSONAL_URL", rootProject.ext.api['local_personal_url']
            manifestPlaceholders = [CHANNEL_VALUE: "none"]
        }

        debug {
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            buildConfigField "String", "DEBUG_URL", rootProject.ext.api['debug_url']
            buildConfigField "String", "DEBUG_PERSONAL_URL", rootProject.ext.api['debug_personal_url']
            manifestPlaceholders = [CHANNEL_VALUE: "none"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            zipAlignEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.release'
            buildConfigField "String", "RELEASE_URL", rootProject.ext.api['release_url']
            buildConfigField "String", "RELEASE_PERSONAL_URL", rootProject.ext.api['release_personal_url']
            manifestPlaceholders = [CHANNEL_VALUE: "none"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }
}

def getVersionCode() {
    def versionFile = file('version.properties')
    if (versionFile.canRead()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(versionFile))
        def versionCode = properties['VERSION_CODE'].toInteger()
        def runTasks = gradle.startParameter.taskNames
        if (':app:assembleRelease' in runTasks || ':app:assembleDebug' in runTasks) {
            properties['VERSION_CODE'] = (++versionCode).toString()
            properties.store(versionFile.newWriter(), null)
        }
        return versionCode
    } else {
        throw new GradleException("Could not find version.properties!")
    }
}

def getVersionName() {
    def versionFile = file('version.properties')
    if (versionFile.canRead()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(versionFile))
        def versionName = properties['VERSION_NAME'].toString()
        return versionName
    } else {
        throw new GradleException("Could not find version.properties!")
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}